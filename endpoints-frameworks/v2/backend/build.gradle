// Copyright 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// If you would like more information on the gradle-appengine-plugin please refer to the github page
// https://github.com/GoogleCloudPlatform/gradle-appengine-plugin

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        /*
            The App Engine standard environment Gradle tooling has been updated.
            In Cloud Endpoints Frameworks 1.0, projects used the `gradle-appengine-plugin`
            plugin in group `com.google.appengine` and migrated or new projects using
            Cloud Endpoints Frameworks 2.0, now use the plugin `appengine-gradle-plugin` in
            group `com.google.cloud.tools`.

            In the previous gradle plugin `gradle-appengine-plugin`, a user was able to generate
            an API Discovery Document and now is generated using the artifact
            `endpoints-framework-gradle-plugin` in group `com.google.cloud.tools`.
        */
        /* [START legacy_plugins]
        classpath 'com.google.appengine:gradle-appengine-plugin:1.9.53'
        [END legacy_plugins] */

        // [START migration_plugins]
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.0'
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.2'
        // [END migration_plugins]
    }
}

repositories {
    jcenter();
}

apply plugin: 'java'
apply plugin: 'war'

/*
    The following two sections represent how Cloud Endpoints Frameworks 1.0
    projects applied plugins in a Gradle build script and how the new plugins
    are applied for Cloud Endpoints Frameworks 2.0 projects.

    The `endpoints-framework-gradle-plugin` provides two plugins to apply given
    a specific scenario Cloud Endpoints Frameworks 2.0 client-side and
    server-side projects.

    Client-side - com.google.cloud.tools.endpoints-framework-client
    Server-side - com.google.cloud.tools.endpoints-framework-server

    This build script uses `com.google.cloud.tools.endpoints-framework-server`
    because it is the server-side of this Cloud Endpoints Frameworks 2.0 project.
*/

/* [START legacy_apply]
apply plugin: 'appengine'
[END legacy_apply] */

// [START migration_apply]
apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
// [END migration_apply]

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    /*
        The following two sections represent dependencies used by
        Cloud Endpoints Frameworks 1.0 projects in a Gradle build
        script and how the new plugins are applied for Cloud Endpoints
        Frameworks 2.0 projects.
    */
    /* [START legacy_dependencies]
    appengineSdk 'com.google.appengine:appengine-java-sdk:1.9.53'
    compile 'com.google.appengine:appengine-endpoints:1.9.53'
    compile 'com.google.appengine:appengine-endpoints-deps:1.9.53'
    [END legacy_dependencies] */

    // [START migration_dependencies]
    compile 'com.google.endpoints:endpoints-framework:2.0.7'
    compile 'javax.inject:javax.inject:1'
    // [END migration_dependencies]

    compile 'javax.servlet:servlet-api:2.5'
}

appengine {  // App Engine tasks configuration
    deploy {   // deploy configuration
        version = findProperty("appengine.deploy.version")
        println version
        def promoteProp = findProperty("appengine.deploy.promote")
        if (promoteProp != null) {
            promote = new Boolean(promoteProp)
        }
    }
}

/*
    The following section was removed as the `gradle-appengine-plugin`
    is not used not used in Cloud Endpoints Frameworks 2.0 projects.
*/
/* [START legacy_appengine_configuration]
appengine {
    downloadSdk = true
    appcfg {
        oauth2 = true
    }
    endpoints {
        getClientLibsOnBuild = true
        getDiscoveryDocsOnBuild = true
    }
}
[END legacy_appengine_configuration] */

